<?xml version="1.0" encoding="utf-8"?>
<s:Group height="40"  
		 alpha="0"
		 creationComplete="onInitView();"
		 xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" >

	<fx:Script>
		<![CDATA[
			import flash.filters.DropShadowFilter;
			import com.asfusion.mate.l10n.events.LocaleEvent;
			import l10n.views.assets.LocaleAssets;
			
			import mx.controls.Image;
			import mx.effects.IEffect;
			
			
			// *********************************************************
			// Overriden Methods 
			// *********************************************************
			
			override protected function createChildren():void {
				super.createChildren();
				buildLocaleButtons();
			}
			
			// *********************************************************
			// EventHandlers 
			// *********************************************************

			private function onInitView():void {
				// Fade in the entire langugage bar...
				var effect: Fade  = this.fadeIn;
				var timer : Timer = new Timer(1300,1);
				
				    timer.addEventListener(	TimerEvent.TIMER,
										  	function(event:TimerEvent):void {
												fadeIn.play();
											},false, 0, true);
					timer.start();
			}
			
			private function onLoadLocale(event:Event=null,src:Image=null):void {
				var img    : Image  = event ? event.currentTarget as Image : src;
				var locale : String = LocaleAssets.getLocaleFor(Class(img.source));
				
				showAsSelected(img);
				dispatchEvent(new LocaleEvent(LocaleEvent.LOAD_LOCALE,locale));
			}
			
			// *********************************************************
			// Private Methods 
			// *********************************************************

			/**
			 * Build the locale image Flags dynamically using the LocaleAssets.
			 * Additions/Modifications to the LocaleAssets will automatically change
			 * the image buttons shown and work with the LocalizationMaps.
			 * 
			 */
			private function buildLocaleButtons():void {
				for (var j:int=0; j<LocaleAssets.allFlags.length; j++) {
					var flag: Class = LocaleAssets.allFlags[j] as Class;
					var img : Image = new Image;
					
						img.buttonMode 	  = true;
						img.useHandCursor = true;
						img.filters 	  = [shadow];
					    img.source        = flag;
						img.toolTip 	  = LocaleAssets.getToolTip(flag);
						img.addEventListener(MouseEvent.CLICK, onLoadLocale);
						
					addElement(img);
					
					// Current OS matches this locale... so simulate click on button
					var locale : String = LocaleAssets.getLocaleFor(flag);
					if (locale.indexOf(Capabilities.language) > -1) {
						onLoadLocale(null,img);
					}
				}				
			}
			
			/**
			 *  Hide the dropShadow on the Flag button for the loaded locale
			 * 
			 *  @param selected Image button which was just "clicked"  
			 */
			private function showAsSelected(selected:Image):void {
				for (var j:int=0; j<numElements; j++) {
					var img : Image = getElementAt(j) as Image;
					if (img == null) continue;
					
					img.filters = (img == selected) ? [] : [shadow];
					img.alpha   = (img == selected) ? .3 : 1;
				}
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<s:Fade 			 id="fadeIn" duration="900" alphaTo="1" target="{this}"  />
		<mx:DropShadowFilter id="shadow" blurX="4" blurY="4" alpha=".4" angle="45" />
	</fx:Declarations>
	
	<s:layout>
		<s:HorizontalLayout horizontalAlign="right" verticalAlign="middle" />
	</s:layout>
	
	<s:Label id="lblBarHelp" text="Switch to locale:" alpha=".5" fontSize="9" />
	<mx:Spacer width="10" height="1" />
	
</s:Group>
